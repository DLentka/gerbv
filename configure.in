dnl configuration script for gerbv
dnl
dnl  gEDA - GNU Electronic Design Automation
dnl  This file is part of gerbv.
dnl
dnl    Copyright (C) 2000-2001 Stefan Petersen (spe@stacken.kth.se)
dnl
dnl  $Id$
dnl
dnl  This program is free software; you can redistribute it and/or modify
dnl  it under the terms of the GNU General Public License as published by
dnl  the Free Software Foundation; either version 2 of the License, or
dnl  (at your option) any later version.
dnl
dnl  This program is distributed in the hope that it will be useful,
dnl  but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl  GNU General Public License for more details.
dnl
dnl  You should have received a copy of the GNU General Public License
dnl  along with this program; if not, write to the Free Software
dnl  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111 USA


AC_INIT(src/gerbv.c)

dnl variables
PACKAGE=gerbv
VERSION=0.14

dnl --enable-exportpng
AC_ARG_ENABLE(exportpng,
    [  --disable-exportpng     Disable PNG export.],
    [
        if test $enableval = "yes"; then
		exportpng=yes
	  	AC_DEFINE(EXPORT_PNG)
	fi
    ],
    [
	exportpng=yes
  	AC_DEFINE(EXPORT_PNG)
    ])

dnl --with-maxfiles : sets maximum number of simultaneous loaded files
AC_ARG_WITH(maxfiles,
    [  --with-maxfiles         Maximum number of simultaneous loaded files (default 20).],
	AC_DEFINE_UNQUOTED(MAX_FILES, $withval),
	AC_DEFINE(MAX_FILES, 20)
)

dnl --enable-unit-mm : Set default unit for coordinates in status bar to mm
AC_ARG_ENABLE(unit-mm,
    [  --enable-unit-mm        Set default unit for coordinates in status bar to mm],
    [
        if test $enableval = "yes"; then
		default_unit="mm"
	fi
    ])
if test "$default_unit" = "mm"; then
	AC_DEFINE(GERBV_DEFAULT_UNIT, GERBV_MMS)
else
	AC_DEFINE(GERBV_DEFAULT_UNIT, GERBV_MILS)
fi


dnl Initialize automake stuff (?)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)

dnl Create a configuration header 
AM_CONFIG_HEADER(config.h)

dnl Build time sanity check... (?)
AM_SANITY_CHECK

dnl Initialize maintainer mode (?)
AM_MAINTAINER_MODE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_ARG_PROGRAM

dnl Change default location of gtk-config
AC_ARG_WITH(gtk-config, [  --with-gtk-config=path  Change where gtk-config is located], [opt_gtkconfig=$withval])

if eval "test x$opt_gtkconfig != x" ; then

        if test ! -r "$opt_gtkconfig"; then

                AC_MSG_ERROR(Cannot find $opt_gtkconfig -- be sure that this pathname is correct)

        else
                GTK_CONFIG=$opt_gtkconfig
        fi
else
        AM_PATH_GTK(1.2.3,,AC_MSG_ERROR([*** GTK+ >= 1.2.3 not installed - please install first ***]))
fi

if test $GTK_CONFIG = "no"; then
        AC_MSG_ERROR(Cannot find gtk-config -- be sure that gtk >= 0.99.7 is installed)
fi

GTK_LIBS="`$GTK_CONFIG --libs`"
GTK_CFLAGS="`$GTK_CONFIG --cflags`"

dnl Why do I have to set prefix?
if eval "test x$prefix = xNONE"; then
	prefix=$ac_default_prefix
fi

dnl GDK-PixBuf stuff
if test "$exportpng" = "yes" ; then
	AM_PATH_GDK_PIXBUF
	AC_CHECK_LIB(png, png_write_info,, 
		AC_MSG_ERROR(*** Must have libpng to get export-png to work))
	PNG_LIBS="-lpng"
else
	GDK_PIXBUF_CFLAGS=
	GDK_PIXBUF_LIBS=
	PNG_LIBS=
fi

dnl This is because I can unquote only once. Should be $datadir/gerbv/scheme
if test -z "$opt_backend_dir"; then
    if eval "test $datadir = '${prefix}/share'"; then
       opt_backend_dir="$prefix/share/gerbv/scheme"
    else
       opt_backend_dir="$datadir/gerbv/scheme"
    fi
fi

AC_DEFINE_UNQUOTED(BACKEND_DIR, "$opt_backend_dir")
BACKEND_DIR="$opt_backend_dir"

AC_DEFINE_UNQUOTED(VERSION, "$VERSION")

AC_CHECK_HEADERS(unistd.h getopt.h string.h)

AC_CHECK_FUNCS(getopt_long)

AC_SUBST(CC)
AC_SUBST(VERSION)

AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_CONFIG)
AC_SUBST(GTK_LIBS)

AC_SUBST(GDK_PIXBUF_CFLAGS)
AC_SUBST(GDK_PIXBUF_LIBS)
AC_SUBST(PNG_LIBS)

AC_SUBST(BACKEND_DIR)

AC_OUTPUT(	Makefile \
		src/Makefile \
		man/Makefile \
		man/gerbv.1)
